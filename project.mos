model ModelName
  options noimplicit
  uses "mmxprs"

  parameters
    n = 10                              ! numero di jobs
    m = 9                               ! numero di tools
    B = 4                               ! max numero tool su macchina
  end-parameters

  forward procedure printParams
  forward procedure printResults

  declarations

    J = 1..n                            ! insieme dei Jobs
    J0 = 0..n                           ! insieme dei Jobs U {0}

    T = 0..m                            ! insieme dei Tools
    T_j : array(J0) of set of integer   ! array di insiemi di Tools necessari per il Job j

    Tc_m : array(T) of integer          ! costi di montaggio tool t
    Tc_s : array(T) of integer          ! costi di smontaggio tool t

    x_ij : array(J0, J0) of mpvar       ! 1 se job 'j' segue immediatamente job 'i' - 0 altrimenti
    z_ij_t : array(J0, J0, T) of mpvar  ! 1 se tool 't' viene aggiunto nel passaggio i->j
    y_ij_t : array(J0, J0, T) of mpvar  ! 1 se tool 't' viene lasciato nel passaggio i->j

    u : array(J0) of mpvar              ! array contenete ordinamento posizionale Jobs

    mintime : linctr

    Job_seq : array(J0) of integer      ! aux var per print solution
    cur_job : integer                   ! aux var per print solution
    seq : integer                       ! aux var per print solution

  end-declarations

  !************** assignement *************! TODO: external file assignment

  initialisations from "data.dat"
    T_j
    Tc_m
    Tc_s
  end-initialisations

  printParams
  
  !************** constraints *************!

  mintime := sum(i,j in J0 | i<>j)(sum(t in T_j(j))(z_ij_t(i,j,t)*(Tc_m(t)+Tc_s(t))))

  ! vincoli su variabili binarie
  forall(i,j in J0 | i<>j)
    x_ij(i,j) is_binary

  forall(i,j in J0 | i<>j, t in T) do
    z_ij_t(i,j,t) is_binary
    y_ij_t(i,j,t) is_binary
  end-do

  ! TSP constraint (1)
  forall(i in J0)
    sum(j in J0-{i})x_ij(i,j) = 1

  ! TSP constraint (2)
  forall(j in J0)
    sum(i in J0-{j})x_ij(i,j) = 1

  ! TSP constraint (3)
  u(0) = 0

  forall(i,j in J | i<>j)
    u(i) - u(j) + n*x_ij(i,j) <= n-1

  forall(i in J) do
    u(i) >= 1
    u(i) <= n
  end-do

  ! SSP constraint (4)
  forall(j in J0, t in T_j(j))
    sum(i in J0-{j})(y_ij_t(i,j,t) + z_ij_t(i,j,t)) = 1

  ! SSP constraint (5)
  forall(i,j in J0 | i<>j, t in T | t not in T_j(i) and t in T_j(j))
    y_ij_t(i,j,t) + z_ij_t(i,j,t) <= x_ij(i,j)

  ! SSP constraint (6)
  forall(i,j in J0 | i<>j)
    sum(t in T)(y_ij_t(i,j,t) + z_ij_t(i,j,t)) <= B*x_ij(i,j)

  ! SSP constraint (7)
  forall(i in J0, t in T-{0})
    sum(k in J0-{i})(y_ij_t(k,i,t) + z_ij_t(k,i,t)) >= sum(j in J0-{i})y_ij_t(i,j,t)

  ! SSP constraint (8 a)
  forall(i,j in J0 | i<>j, t in T_j(i) | t in T_j(j))
    y_ij_t(i,j,t) >= x_ij(i,j)

  ! SSP constraint (8 b)
  forall(j in J, t in T-{0})
    y_ij_t(0,j,t) = 0

  ! SSP constraint (9)
  forall(i,j in J0 | i<>j, t in T-{0} | t not in T_j(j))
    z_ij_t(i,j,t) = 0

  minimize(mintime)

 printResults

  !************** procedures *************!

  procedure printParams
    write("\n")
    write("\n- Insieme dei Jobs 'J': ")
      forall(j in J)
        write(j, " ")

    write("\n- Insieme dei Jobs 'J0': ")
      forall(j in J0)
        write(j, " ")

    write("\n- Insieme dei Tools per:")
    forall(j in J0)
      write("\n\tJob ", j, ": ", T_j(j))

    write("\n- Costi di montaggio:")
    forall(t in T)
      write("\n\tTool ", t, ": ", Tc_m(t))

    write("\n- Costi di smontaggio:")
    forall(t in T)
      write("\n\tTool ", t, ": ", Tc_s(t))
  end-procedure


  procedure printResults

    write("\n")

    ! Print sequenza Job sotto forma di vettore
    cur_job := 0
    seq := 0
    Job_seq(seq) := cur_job
    seq := 1
    forall(i in 0..n-1) do
      forall(j in 0..n) do
        if getsol(x_ij(cur_job,j)) > 0 then
          cur_job := j
          Job_seq(seq) := j
          seq := seq+1
          break
        end-if
      end-do

    end-do
    write("\n - Job sequence:\n\t")
    forall(i in J0) do
      write(Job_seq(i))
      if i<n then
        write("->")
      end-if
    end-do

    write("\n- Tool sequence:\n")
    forall(i in J0) do
      write("\t")
      write("J",Job_seq(i),": ",T_j(Job_seq(i)))
      write("\n")
    end-do

    ! Print mintime solution
    write("- mintime: ",getobjval)
  end-procedure


end-model
